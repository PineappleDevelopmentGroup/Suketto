plugins {
    id "java"
    id "com.github.johnrengelman.shadow" version "7.1.0"
    id "io.freefair.aggregate-javadoc-jar" version "8.0.1"
    id 'maven-publish'
}

group 'sh.miles.suketto'
version '1.19.4'

compileJava.options.encoding = "UTF-8"
compileTestJava.options.encoding = "UTF-8"
jar.enabled = false

subprojects {
    apply plugin: 'java'
    apply plugin: 'maven-publish'

    repositories {
        mavenLocal()
        mavenCentral()
        maven {
            name = "spigotmc-repo"
            url = "https://hub.spigotmc.org/nexus/content/repositories/snapshots/"
        }
    }

    tasks.register('javadocJar', Jar) {
        dependsOn javadoc
        archiveClassifier.set('javadoc')
        from javadoc.destinationDir
    }

    tasks.named('test', Test) {
        useJUnitPlatform()

        maxHeapSize = '8G'

        testLogging {
            events "passed"
        }
    }

    compileJava {
        options.release = 17
    }

    test {
        dependsOn cleanTest
        testLogging.showStandardStreams = true
    }

    def javadocConfig = {
        options {
            links 'https://hub.spigotmc.org/javadocs/spigot/'
            addStringOption('Xdoclint:none', '-quiet')
            tags "nms:a:Requires NMS support"
            exclude "com/jeff_media/lunatic/internal/**"
            exclude "com/jeff_media/lunatic/nms/**"
        }
    }

    dependencies {
        compileOnly 'org.jetbrains:annotations-java5:23.0.0'
        testImplementation 'org.junit.jupiter:junit-jupiter:5.7.1'
    }

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(17)
        }
    }

    javadoc {
        configure javadocConfig
    }

    aggregateJavadoc() {
        configure javadocConfig
    }

    String moduleName = it.name
    moduleName = "suketto-" + moduleName

    publishing {
        publications {
            maven(MavenPublication) { publication ->
                groupId = this.group
                artifactId = moduleName
                version = this.version

                publication.artifact(jar)
                publication.artifact(javadocJar)
            }
        }
        repositories {
            maven {
                credentials {
                    username = "$milesMavenUser"
                    password = "$milesMavenPass"
                }
                url = "https://maven.miles.sh/libraries"
            }
        }
    }
}

tasks.named('aggregateJavadoc', Javadoc) {
    options {
        title "Lunatic " + project.version
        overview "javadoc/overview.html"
    }
}

dependencies {
    implementation project(':core')
    implementation project(':bukkit')
    implementation project(':nms:base')
    implementation project(path: ':nms:v1_19_4', configuration: 'reobf')
    implementation project(path: ':nms:v1_20_1', configuration: 'reobf')
}

shadowJar {
    archiveClassifier.set('')
}

build {
    dependsOn(shadowJar, aggregateJavadoc)
}

publishing {
    publications {
        shadow(MavenPublication) { publication ->
            project.shadow.component(publication)
            publication.artifact(aggregateJavadocJar)
        }
    }
    repositories {
        maven {
            credentials {
                username = "$milesMavenUser"
                password = "$milesMavenPass"
            }
            url = "https://maven.miles.sh/libraries"
        }
    }
}

aggregateJavadocJar {
    getDestinationDirectory().set(file(getBuildDir().toPath().resolve("docs")))
}
